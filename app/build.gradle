apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply greenDao plugin
apply from: 'and-res-guard.gradle'

def keyFile = file(findProperty("tianma.keystore.path") as String)
def propertyFile = file(findProperty("tianma.signature.path") as String)

def keyProps = new Properties()
if (propertyFile.exists()) {
    keyProps.load(new FileInputStream(propertyFile))
}

def static releaseTime() {
    return new Date().format("yyMMdd", TimeZone.default)
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.github.tianma8023.xposed.smscode"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 42
        versionName "2.2.2"

        buildConfigField("String", "LOG_TAG", "\"XSmsCode\"")
        buildConfigField("int", "MODULE_VERSION", "42")

        // vectorDrawables.generatedDensities = ['hdpi']

        resConfigs("en", "zh-rCN", "zh-rTW")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ eventBusIndex : 'com.tianma.xsmscode.data.eventbus.MyEventBusIndex' ]
            }
        }
    }

    signingConfigs {
        release {
            storeFile keyFile
            storePassword keyProps['STORE_PASSWORD']
            keyAlias keyProps['KEY_ALIAS']
            keyPassword keyProps['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            buildConfigField("int", "LOG_LEVEL", "2")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
            buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")

            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }
        }

        alpha {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable  true

            buildConfigField("int", "LOG_LEVEL", "2")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
            buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")

            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField("int", "LOG_LEVEL", "4")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
            buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")
            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "XposedSmsCode_v${variant.versionName.replaceAll('\\s+', '_')}_${releaseTime()}_${variant.buildType.name.charAt(0)}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

greendao {
    schemaVersion 8
}

ext {
    butterKnifeVersion = "10.1.0" // butter knife

    materialDialogVersion = "0.9.6.0" // material dialog version

    andriodShellVersion = "1.0.0" // Android Shell

    eventBusVersion = "3.1.1" // event bus

    greenDaoVersion = "3.2.2" // green dao
    greenDaoHelperVersion = "v2.1.0" // green dao helper

    gsonVersion = "2.8.5" // gson

    dagger2Version = "2.23.2" // dagger 2

    rxJavaVersion = "2.2.10" // RxJava 2
    rxAndroidVersion = "2.1.1" // RxAndroid 2

    okHttpVersion = "3.14.2" // OkHttp
    retrofitVersion = "2.5.0" // retrofit

    jsoupVersion = "1.12.1" // jsoup

    cyaneaVersion = "1.0.2" // cyanea
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0" // material design support
    implementation "androidx.recyclerview:recyclerview:1.0.0" // recycler view
    implementation "androidx.cardview:cardview:1.0.0" // card view
    implementation "androidx.browser:browser:1.0.0" // custom tabs
    implementation "androidx.preference:preference:1.0.0" // preference support
    implementation "androidx.constraintlayout:constraintlayout:1.1.3" // constraint layout

    // ButterKnife
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    // Xposed
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    // Material Dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogVersion"

    // Android Shell
    implementation "com.jaredrummler:android-shell:$andriodShellVersion"

    // EventBus
    implementation "org.greenrobot:eventbus:$eventBusVersion"
    // EventBus annotation processor
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:$eventBusVersion"

    // GreenDao
    implementation "org.greenrobot:greendao:$greenDaoVersion"
    // GreenDao Upgrade Helper
    implementation "com.github.yuweiguocn:GreenDaoUpgradeHelper:$greenDaoHelperVersion"

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // dagger2
    implementation "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"
    // dagger2-android
    implementation "com.google.dagger:dagger-android:$dagger2Version"
    implementation "com.google.dagger:dagger-android-support:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger2Version"

    // RxJava 2
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    // RxAndroid 2
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    // OkHttp logging interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    // Retrofit Gson converter
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    // Retrofit scalars converter
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    // Retrofit RxJava Adapter
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // JSoup
    implementation "org.jsoup:jsoup:$jsoupVersion"

    // Cyanea
    implementation "com.jaredrummler:cyanea:$cyaneaVersion"
}
